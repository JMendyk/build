\section{Build systems, abstractly}\label{sec-abstractions}

This section presents purely functional abstractions that allow us to express
all the intricacies of build systems discussed in the previous
section~\S\ref{sec-background} and design complex build systems from simple
primitives. Specifically, we present the \emph{task} and \emph{build}
abstractions in~\S\ref{sec-task} and~\S\ref{sec-general-build}, respectively.
Sections~\S\ref{sec-build} and~\S\ref{sec-implementations} scrutinise the
abstractions further and provide concrete implementations for several build
systems.

We start by establishing a common vocabulary for build systems that allows
us to abstract away from specific application domains, such as software
development or spreadsheets.

\subsection{Common vocabulary for build systems}\label{sec-vocabulary}

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
-- An abstract store
data Store i k v
getInfo    :: Store i k v -> i
putInfo    :: Store i k v -> i -> Store i k v
getValue   :: Store i k v -> k -> v
putValue   :: (Eq k, Hashable v) => Store i k v -> k -> v -> Store i k v
getHash    :: Hashable v => Store i k v -> k -> Hash
initialise :: Hashable v => i -> (k -> v) -> Store i k v
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Hashing
hash :: Hashable a => a -> Hash
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Applicative functors
pure  :: Applicative f => a -> f a
(<$>) :: Applicative f =>   (a -> b) -> f a -> f b -- Left-associative
(<*>) :: Applicative f => f (a -> b) -> f a -> f b -- Left-associative
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- State monad
data State s a
instance Applicative (State s)
instance Monad       (State s)
get       :: State s s
put       :: s -> State s ()
modify    :: (s -> s) -> State s ()
execState :: State s a -> s -> s
\end{minted}
\vspace{-2mm}
\caption{Signatures of main data types and library functions.}\label{fig-types}
\vspace{-4mm}
\end{figure}

A \emph{build system} takes a \emph{task description}, a target \emph{key},
and a \emph{store}, and returns a new store in which the target key is
up-to-date. The store is a key-value map, together with \emph{persistent build
information} that is maintained by the build system itself.

In software build systems, keys are typically filenames,
e.g.~\cmd{main.c}, and values are file contents (a C program source
code in this case). In spreadsheets, keys are cell names,
e.g. \cmd{A1}, and values are numbers, text, etc. that are typically
displayed inside cells. In our Haskell code, we will use type
variables \hs{k} and \hs{v} to denote keys and values, respectively.
Fig.~\ref{fig-types} provides a Haskell encoding of our main definitions,
and gives types of the library functions we use in this paper.

Some values must be provided by the user as \emph{input}. For example,
\cmd{main.c} can be edited by the user who relies on the build system to
compile it into \cmd{main.o} and subsequently \cmd{main.exe}. End build products,
such as \cmd{main.exe}, are \emph{output} values. All other values (in this case
\cmd{main.o}) are \emph{intermediate}; they are not interesting for the user
but are produced in the process of turning inputs into outputs.

We use a cryptographic \emph{hash function}
\hs{hash}~\hs{::}~\hs{Hashable}~\hs{v}~\hs{=>}~\hs{v}~\hs{->}~\hs{Hash} for
efficient tracking and sharing of build results, where \hs{Hash} is an abstract
data type equipped with the equality test.

A \emph{store}, of type \hs{Store}~\hs{i}~\hs{k}~\hs{v}, associates keys to
values. It is convenient to assume that a store is total, i.e. it contains a
value for every possible key. We therefore also assume that the type of values
is capable of encoding values corresponding to non-existent keys (missing files
or empty cells). In addition to usual \hs{getValue} and \hs{putValue} operations,
some stores support a dedicated \hs{getHash} operation, which is implemented
more efficiently than by hashing the result of \hs{getValue}\footnote{For
example, Git Virtual File System~\cite{gvfs} stores file hashes and downloads
file contents only on demand.}. The store also holds a value of type \hs{i},
representing the persistent build information maintained by the build
system~--~its ``memory''. Given an \hs{i} and a key-value map \store, we can
\hs{initialise} a store.

% This, in particular, suggests that it is possible to \emph{depend on the
% absence of a value}, which is a useful feature for build systems.

\subsection{The Task abstraction}\label{sec-task}

Our first main idea is the task-description abstraction:
\begin{minted}[xleftmargin=10pt]{haskell}
type Task c k v = @\std{forall}@ f. c f => (k -> f v) -> k -> Maybe (f v)
\end{minted}
This highly-abstracted type\footnote{Readers familiar with \emph{lenses} or
\emph{profunctor optics} might recognise a familiar pattern. We discuss this
in~\S\ref{sec-related-optics}.} is best introduced by an example.
Consider this \Excel spreadsheet:
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{text}
  A1: 10     B1: A1 + A2
  A2: 20     B2: B1 * 2
\end{minted}
\vspace{1mm}
Here cell \cmd{A1} contains the value \cmd{10}, the cell \cmd{B1} contains
the formula \cmd{A1+A2}, etc. We can represent the formulae of this spreadsheet
with the following task description:
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
sprsh1 :: Task Applicative String Integer
sprsh1 fetch "B1" = Just ((+)   <$> fetch "A1" <*> fetch "A2")
sprsh1 fetch "B2" = Just ((* 2) <$> fetch "B1")
sprsh1 _     _    = Nothing
\end{minted}
\vspace{1mm}
Here we instantiate keys \hs{k} with \hs{String}, and values \hs{v} with \hs{Integer}.
(Real spreadsheet cells would contain a wider range of values, of course.)
The task description \hs{sprsh1} embodies all the \emph{formulae} of the spreadsheet,
but not the input values.  Like every \hs{Task}, \hs{sprsh1} is given a
\hs{fetch} function and a key. It pattern-matches on the key to see if it has a
task description (in the \Excel case, a formula) for it. If not, it returns
\hs{Nothing}, indicating the key is an input. If there is a formula in the cell,
it computes the value of the formula, using \hs{fetch} to find the value of any
keys on which it depends.

The code to ``compute the value of a formula'' in \hs{sprsh1} looks a bit mysterious
because it takes place in an \hs{Applicative} computation \cite{mcbride2008applicative}
-- the relevant type signatures are given in Fig.~\ref{fig-types}. We will
return to the need for this generality in~\S\ref{sec-deps}.

For now, we content ourselves with observing that a task description,
of type \hs{Task}~\hs{c}~\hs{k}~\hs{v}, is completely isolated from the world of
compilers, calculation chains, file systems, caches, and all other
complexities of real build systems.  It just computes a single output, in
a side-effect-free way, using a callback (\hs{fetch}) to find the values
of its dependencies.

\subsection{The Build abstraction}\label{sec-general-build}

Next comes our second main abstraction -- a build system:
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
type Build c i k v = Task c k v -> k -> Store i k v -> Store i k v
\end{minted}
\vspace{1mm}
The signature is very straightforward.  Given a task description, a target key,
and a store, the build system returns a new store in which the value of the
target key is up-to-date. What exactly does ``up-to-date'' mean?  We answer
that precisely in \S\ref{sec-build-correctness}. Here is a simple build system:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
busy :: (Eq k, Hashable v) => Build Monad () k v
busy task key store = execState (compute key) store
  where
    compute k = case task compute k of
        Nothing  -> do { s <- get; return (getValue s k) }
        Just act -> do { v <- act; modify (\s -> putValue s k v); return v }
\end{minted}
\vspace{1mm}

\noindent
The \hs{busy} build system uses a function
\hs{compute}~\hs{::}~\hs{k}~\hs{->}~\hs{State}~\hs{(Store}~\hs{i}~\hs{k}~\hs{v)}~\hs{v},
which given a key brings the associated value up-to-date and returns it. To do
so, \hs{compute} calls \hs{task} passing itself as an argument, so that \hs{task}
can use \hs{compute} to recursively find the values of its dependencies. If
\hs{task} returns \hs{Nothing} (this key is an input), \hs{compute} simply reads
the result from the store~\hs{s}, which is available in the \hs{State} monad (in
which \hs{compute} runs) via the \hs{get} method~--~see the \hs{State} monad
interface in Fig.~\ref{fig-types}. Otherwise it runs the action \hs{act}
returned by the \hs{task} to produce a resulting value~\hs{v}, stores it
(modifying the store \hs{s}), and returns it.

Given an acyclic task description, the \hs{busy} build system terminates with a
correct result, but it is not a minimal build system (c.f. the
Definition~\ref{def-minimal}). Since \hs{busy} has no memory (\hs{i = ()}), it
cannot keep track of keys it has already built, and will therefore busily
recompute the same keys again and again if they have multiple dependents. We
will develop much more efficient build systems in~\S\ref{sec-implementations}.

Despite limitations, \hs{busy} can easily handle the example \hs{sprsh1}
from the previous subsection~\S\ref{sec-task}. Below we initialise the store with
\cmd{A1} set to 10 and all other cells set to 20.

\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ store  = initialise () (\key -> if key == "A1" then 10 else 20)
@\ghci@ result = busy sprsh1 "B2" store
@\ghci@ getValue result "B1"
30
@\ghci@ getValue result "B2"
60
\end{minted}

\noindent
As one can see, \hs{busy} built both \cmd{B2} and its dependency \cmd{B1} in the
right order (if it had built \cmd{B2} before building \cmd{B1}, the result would
have been $20 * 2 = 40$ instead of $(10 + 20) * 2 = 60$). As an example showing
that \hs{busy} is not minimal, imagine that the formula in cell \cmd{B2} was
\cmd{B1~+~B1} instead of \cmd{B1~*~2}. This would lead to calling
\hs{compute}~\hs{"B1"} twice -- once per each occurrence of \cmd{B1} in the
formula.

An alert reader might notice that \hs{busy} accepts \emph{monadic tasks} of
type \hs{Task}~\hs{Monad}, whereas \hs{sprsh1} is a \hs{Task}~\hs{Applicative}.
This indicates that \hs{busy} can cope with tasks containing dynamic
dependencies, which we discuss in the next subsection~\S\ref{sec-task-monad}.

It is interesting to note that if \hs{busy} is given a \emph{functorial task}
of type \hs{Task}~\hs{Functor}, which cannot use the application operator
\hs{<*>}, then \hs{busy} can rejoice in knowing that it will never have to
rebuild a key twice. Indeed, functorial tasks are very much like tail recursive
functions -- they cannot branch into multiple subproblems, always leading to a
straight-line build.

\subsection{Monadic tasks}\label{sec-task-monad}

As explained in~\S\ref{sec-background-excel}, some build tasks have dynamic
dependencies, which are determined by values of intermediate computations. In
our framework, such build tasks correspond to the type
\hs{Task}~\hs{Monad}~\hs{k}~\hs{v}. Recall the following spreadsheet
example:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{text}
A1: 10      B1: IF(C1=1,A1,A2)      C1: 1
A2: 20
\end{minted}

\noindent
The task corresponding to the cell \cmd{B1} statically depends on \cmd{C1} and
dynamically depends on either \cmd{A1} or \cmd{A2}. We can express this using
our task abstraction as follows\footnote{The spreadsheet example that uses
the \hs{INDIRECT} function can be expressed very similarly: simply replace the
line containing the \cmd{if} statement with \hs{fetch ("A" ++ show c1)}.}:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
sprsh2 :: Task Monad String Integer
sprsh2 fetch "B1" = Just $ do c1 <- fetch "C1"
                              if c1 == 1 then fetch "A1" else fetch "A2"
sprsh2 _     _    = Nothing
\end{minted}
\vspace{1mm}

\noindent
The main difference compared to \hs{sprsh1} is that the computation now takes
place in a \hs{Monad}, which allows us to extract the value of \hs{c1} and
\hs{fetch} different keys depending on whether \hs{c1}~\hs{==}~\hs{1} or not.

Since the \hs{busy} build system introduced in~\S\ref{sec-general-build} always
rebuilds every dependency it encounters, it is easy for it to handle dynamic
dependencies. For minimal build systems, however, dynamic dependencies and hence
monadic tasks are much more challenging, as we will see
in~\S\ref{sec-implementations}.

\subsection{Correctness of a build system} \label{sec-build-correctness}

We can now say what it means for a build system to be \emph{correct}, something
that is seldom stated formally:

\definition[Correctness]{Suppose \hs{build} is a build system, \hs{task} is a
build task description, \hs{key} is an output key, \hs{store} is an initial
store, and \hs{result} is the store produced by running the build system with
parameters \hs{task}, \hs{key} and \hs{store}. Or, using the precise language of
our abstractions:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
build  :: Build c i k v
task   :: Task c k v
key    :: k
store  :: Store i k v
result :: Store i k v
result = build task key store
\end{minted}
\vspace{1mm}

\noindent
Then the \hs{result} is~\emph{correct} if there exists an \hs{ideal} store, such
that:

\begin{itemize}
    \item The \hs{ideal} store is \emph{consistent} with the \hs{task}, i.e.
    for all possible non-input keys \hs{k}, the result of executing the
    \hs{task} matches the value in the \hs{ideal} store:
    \[
    \hs{execute}~\hs{task}~\hs{ideal}~\hs{k}~\hs{==}~\hs{Just}~\hs{(}\hs{ideal}~\hs{k)}.
    \]
    \item The \hs{result} and \hs{ideal} agree on the output \hs{key}, i.e.
    \hs{result}~\hs{key}~\hs{==}~\hs{ideal}~\hs{key}. In other words, we require
    that the output \hs{key} has the correct value, but do not impose any
    restrictions on intermediate keys, therefore permitting shallow cloud builds.
    \item The \hs{store}, \hs{ideal} and \hs{result} all agree on the inputs
    that belong to the transitive closure of \hs{key}'s dependencies. This means
    that (i) no inputs were corrupted during the build, and (ii)~the \hs{ideal}
    store has constraints not only on the output, but also on the inputs.
\end{itemize}
}
\label{def-correct}

\subsection{Computing dependencies}\label{sec-deps}

The \hs{Task} type and the example above might seem mysterious. Why insist on
\hs{Task} to be polymorphic in \hs{f}? Why choose the \hs{Applicative}
constraint?

The answer to both questions is that we can statically analyse an applicative
compute and extract all dependencies of a key using the following function:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
import Data.Functor.Const
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
dependencies :: Task Applicative k v -> k -> [k]
dependencies task = maybe [@@] getConst . task (\k -> Const [k])
\end{minted}
\vspace{1mm}

\noindent
We can use this function to extract dependencies of \hs{add}, as shown in the
following GHCi session:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ dependencies add "A1"
[@@]
\end{minted}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ dependencies add "B1"
["A1", "A2"]
\end{minted}
\vspace{1mm}

\noindent
It is important to note that we extract the dependencies without providing any
values to the compute. This guarantees that no actual computation is performed
during the analysis -- we cannot execute the function \hs{(+)} if we have no
values for it! Static analysis of applicative computations is not new and has
been used to similar effect before, e.g. see~\cite{free-applicatives}.

To understand how \hs{dependencies} works, let us examine its type more closely,
expanding the type synonym \hs{Task}:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
(@\std{forall}@ f. Applicative f => (k -> f v) -> k -> Maybe (f v)) -> k -> [k]
\end{minted}
\vspace{1mm}

\noindent
Since the first argument is polymorphic in \hs{f}, we can choose any \hs{f} that
satisfies the \hs{Applicative} constraint. A good choice in this case is the
applicative functor \hs{Const}:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
newtype Const m a = Const { getConst :: m }
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Functor (Const m) where
    fmap _ (Const m) = Const m
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
instance Monoid m => Applicative (Const m) where
    pure _              = Const mempty
    Const x <*> Const y = Const (x <> y)
\end{minted}
\vspace{1mm}

\noindent
A value of type \hs{Const}~\hs{[}\hs{k]}~\hs{v} contains no values \hs{v} but
allows us to record all invocations of the custom \hs{fetch} function that we
feed to the given compute: \hs{fetch k = Const [k]}. The compute returns a
value of type \hs{Maybe}~\hs{(Const}~\hs{[}\hs{k]}~\hs{v)} that can be
unwrapped using \hs{maybe [@@] getConst}, i.e. returning the empty list of
dependencies in case of an input.

We can also \hs{execute} a compute on a given store. This relies on a similar
transformation, now based on the \hs{Identity} monad (see
Appendix~\S\ref{sec-appendix-transformers} for the implementation of
\hs{Identity}):

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
import Data.Functor.Identity
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
execute :: Task Monad k v -> (k -> v) -> k -> Maybe v
execute compute store = fmap runIdentity . compute (pure . store)
\end{minted}
\vspace{1mm}

\noindent
Note that \hs{execute} can accept both an applicative as well as a \emph{monadic
task} introduced in~\S\ref{sec-task-monad}. Below we run \hs{execute} on
the above \hs{add} example. Note that \hs{execute} returns \hs{Nothing} for
inputs.

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ store key = if key == "A1" then 10 else 20
@\ghci@ execute add store "A1"
Nothing
\end{minted}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ execute add store "B1"
Just 30
\end{minted}

\subsection{Left-over stuff, need to relocate}

We cannot statically analyse \hs{select} using the function \hs{dependencies}
from the previous section, but we can still \hs{execute} it on a given store:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ store key = if key == "A1" then 10 else 20
@\ghci@ execute select store "B1"
Just 20
\end{minted}
\vspace{1mm}

\noindent
In the example above $\cmd{C1} \neq 1$, hence the \hs{else} branch is taken.
To extract the dependencies of \hs{select} we introduce the function \hs{track}:
a combination of \hs{execute} and \hs{dependencies} that takes the store~\store
as input and computes both the resulting value and the list of its dependencies:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
import Control.Monad.Writer
\end{minted}
\vspace{0.5mm}
\begin{minted}[xleftmargin=10pt]{haskell}
track :: Task Monad k v -> (k -> v) -> k -> Maybe (v, [k])
track task store = fmap runWriter . task (\k -> writer (store k, [k]))
\end{minted}
\vspace{1mm}

\noindent
The implementation uses the \hs{Writer} monad (\S\ref{sec-appendix-transformers}):
we feed \hs{fetch k = writer (store k, [k])} to the compute, which in addition
to fetching a value from the store, tracks the corresponding key. We can
\hs{track} both applicative and monadic compute:

\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ store key = if key == "A1" then 10 else 20
@\ghci@ track add store "B1"
Just (30,["A1","A2"])
\end{minted}
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ track select store "B1"
Just (20,["C1","A2"])
\end{minted}
\vspace{1mm}
\begin{minted}[xleftmargin=10pt]{haskell}
@\ghci@ store' key = if key == "A1" then 10 else 1
@\ghci@ track select store' "B1"
Just (10,["C1","A1"])
\end{minted}
\vspace{1mm}

\noindent
As expected, the dependencies of \hs{select} are determined by the contents of
the store.

% \subsection{Recognising an applicative compute wearing a monadic hat}

% Although dynamic dependencies are very useful, they typically represent only a
% small fraction of all dependencies. One can exploit this in a build system by
% recognising applicative computations even though they have the type
% \hs{Compute}~\hs{Monad} and treating them specially.

% Consider the following data type.

% \subsection{Free build structures}\label{sec-free-build}

% \begin{minted}{haskell}
% data Depend k v r = Depend [k] ([v] -> r)

% data Depends k v r = Depends [k] ([v] -> Depends k v r)
%                    | Done r
% \end{minted}


