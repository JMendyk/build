\section{Build systems, concretely}\label{sec-implementations}

In the previous sections we discussed the types of build systems, and how they
can be broken down. But these divisions were not obvious to us, and only by
concretely implementing and refactoring each build system did we determine the
underlying commonalities. In this section we share some of the code that got us
there.

\subsection{\Make}\label{sec-implementation-make}

We provide an implementation of \Make using our framework in
Fig.~\ref{fig-make-implementation}. \Make processes keys in a linear order based
on a topological sort (see~\S\ref{sec-parallelism} for parallel \Make). For
each key, it builds it if it is older than any of its dependencies. We capture
the persistent build information that \Make stores by a pair
\hs{(@@modTime,}~\hs{now)} comprising the \emph{file modification time} function
\hs{modTime}~\hs{::}~\hs{k}~\hs{->}~\hs{Time} and the \emph{current time} \hs{now}.
Setting aside the explicit manipulation of file modification times, which in
reality is taken care of by the file system, the function \hs{make} captures
the essence of \Make in a clear and precise manner.

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
-- Build information: current time and file modification times
type Time       = Integer
type MakeInfo k = (Time, Map k Time)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Make build system
make :: Ord k => Build Applicative (MakeInfo k) k v
make = topological modTimeRebuilder
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Topological scheduler
topological :: Ord k => Rebuilder Applicative i k v -> Build Applicative i k v
topological rebuilder tasks key = execState $ forM_ chain $ \k -> case tasks k of
    Nothing   -> return ()
    Just task -> do
        value <- gets (getValue k)
        let newTask :: Task (MonadState i) k v
            newTask = rebuilder k value task
            newFetch :: k -> StateT i (State (Store i k v)) v
            newFetch = lift . gets . getValue
        info <- gets getInfo
        (newValue, newInfo) <- runStateT (run newTask newFetch) info
        modify $ putInfo newInfo . putValue k newValue
  where
    deps k = case tasks k of { Nothing -> []; Just task -> dependencies task }
    chain  = case topSort (reachable deps key) of
        Nothing -> error "Cannot build tasks with cyclic dependencies"
        Just xs -> xs
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Standard graph algorithms (implementation omitted)
reachable :: Ord k => (k -> [k]) -> k -> Graph k
topSort   :: Ord k => Graph k -> Maybe [k]
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- A task rebuilder based on file modification times
modTimeRebuilder :: Ord k => Rebuilder Applicative (MakeInfo k) k v
modTimeRebuilder key value task = Task $ \fetch -> do
    (now, modTime) <- get
    let dirty = case Map.lookup key modTime of
            Nothing -> True
            time -> any (\d -> Map.lookup d modTime > time) (dependencies task)
    if not dirty
    then return value
    else do
        put (now + 1, Map.insert key now modTime)
        run task fetch
\end{minted}
\vspace{-2mm}
\caption{An implementation of \Make using our framework.}\label{fig-make-implementation}
\vspace{-2mm}
\end{figure}

The helper function \hs{topological} calls \hs{process} on every \hs{key} in
a topological order, providing the list of its dependencies \hs{deps} and the
action \hs{act} to compute the resulting value if it needs to be rebuilt. To
determine if the \hs{key} is \hs{dirty}, \hs{process} compares its modification
time with those of its dependencies. If the \hs{key} needs to be rebuilt, the
action \hs{act} is executed and the obtained result is stored, along with an
updated file modification timestamp.

The implementation of \hs{topological} encodes the dependency strategy that
\Make has chosen to use. The \hs{where} clause corresponds to the pre-processing
stage, which uses the function \hs{dependencies}, defined in~\S\ref{sec-deps},
to extract static dependencies from the provided applicative \hs{task}. We
compute the linear processing \hs{chain} by taking the keys \hs{reachable}
from \hs{key} via dependencies, and performing the topological sort of the
result. We omit implementation of textbook graph algorithms
\hs{reachable} and \hs{topSort}, e.g.
see~\cite{cormen2001introduction}.

The \hs{chain} is processed in the \hs{State} monad, with each non-input key
\hs{k} in the chain passed to the provided \hs{process} function, along with
\hs{k}'s dependencies and the action \hs{act}, which when executed recomputes
the \hs{k}'s value by fetching its dependencies from the store.

Note that \hs{dependencies} is only defined for applicative tasks, which is what
restricts \Make to static dependencies, as reflected in the
type~\hs{Build}~\hs{Applicative}. Moreover, any other build system following
the same \hs{topological} approach will also inherit the same restriction.

\subsection{\Excel}\label{sec-implementation-excel}

We define \Excel, with its reordering dependency strategy, in  Fig.~\ref{fig-excel-implementation}.
\Excel's persistently stored information is a triple: (i) the dirty bit
function \hs{k}~\hs{->}~\hs{Bool}, (ii) an approximation of key dependencies
\hs{k}~\hs{->}~\hs{ApproximateDependencies}~\hs{k} that \Excel uses to handle
dynamic dependencies, and (iii) the calculation chain \hs{[@@k]} recorded in the
previous build (\S\ref{sec-background-excel}).

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
-- Approximate task dependencies: a map of known upper bounds of dependencies
type ApproximateDependencies k = Map k [k]
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Build information: dirty keys, approximate dependencies, and calculation chain
type CalcChain k = [k]
type ExcelInfo k = ((Set k, ApproximateDependencies k), CalcChain k)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Excel build system
excel :: Ord k => Build Monad (ExcelInfo k) k v
excel = restarting approximateRebuilder
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Restarting scheduler
restarting :: Ord k => Rebuilder Monad i k v -> Build Monad (i, Chain k) k v
restarting rebuilder tasks key = execState $ do
    chain    <- gets (snd . getInfo)
    newChain <- go Set.empty $ chain ++ [key | key `notElem` chain]
    modify . mapInfo $ \(i, _) -> (i, newChain)
  where
    go :: Set k -> Chain k -> State (Store (i, Chain k) k v) (Chain k)
    go _    []     = return []
    go done (k:ks) = case tasks k of
        Nothing -> (k :) <$> go (Set.insert k done) ks
        Just task -> do
            store <- get
            let value = getValue k store
                newTask :: Task (MonadState i) k (Either k v)
                newTask = try $ rebuilder k value task -- `try' is defined below
                newFetch :: k -> State i (Either k v)
                newFetch k | k `Set.member` done = return $ Right (getValue k store)
                           | otherwise           = return $ Left k
            case runState (run newTask newFetch) (fst $ getInfo store) of
                (Left dep, _) -> go done $ [ dep | dep `notElem` ks ] ++ ks ++ [k]
                (Right newValue, newInfo) -> do
                    modify $ putInfo (newInfo, []) . updateValue k value newValue
                    (k :) <$> go (Set.insert k done) ks
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Convert a total task into a task accepting a partial fetch callback
try :: Task (MonadState i) k v -> Task (MonadState i) k (Either e v)
try task = Task $ \fetch -> runExceptT $ run task (ExceptT . fetch)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Rebuilder based on approximate dependencies
approximateRebuilder :: Ord k => Rebuilder Monad (Set k, ApproximateDependencies k) k v
approximateRebuilder key value task = Task $ \fetch -> do
    (isDirty, deps) <- get
    let dirty = key `Set.member` isDirty || case Map.lookup key deps of
                                                Nothing -> True
                                                Just ks -> any (`Set.member` isDirty) ks
    if not dirty
    then return value
    else do
        put (Set.insert key isDirty, deps)
        run task fetch
\end{minted}
\vspace{-2mm}
\caption{An implementation of \Excel using our framework.}\label{fig-excel-implementation}
\vspace{-2mm}
\end{figure}

The helper function \hs{reordering}, whose implementation we omit since it is
technical and not particularly enlightening, calls the function \hs{process} to
\emph{try to build} a \hs{key} by executing the action \hs{act}, in the order
determined by the calculation chain. To decide whether to \hs{rebuild} the
\hs{key}, \hs{process} checks if the \hs{key} itself is marked dirty or the
approximation of its dependencies contains a dirty key. Notice that if the
dependencies of the \hs{key} are \hs{Unknown}, e.g. when it uses the
\cmd{INDIRECT} function, the \hs{key} is always rebuilt. If a \hs{rebuild} is
not needed, \hs{process} returns \hs{Nothing} to indicate that. Otherwise, it
executes \hs{act} leading to one of two possible \hs{result}s:

\begin{itemize}
    \item \hs{MissingDependency}~\hs{k} indicates that the execution of \hs{act} has
    failed, because one of its dependencies \hs{k} was out-of-date, i.e. the
    calculation chain from the previous build was incorrect and therefore needs
    to be reordered, deferring the \hs{key} to be rebuilt later.
    \item \hs{Result}~\hs{v}~\hs{dynamicDependencies} indicates that the
    execution has succeeded producing the value~\hs{v} and the list of the
    \hs{key}'s dynamic dependencies. We store the value, and mark it dirty to
    trigger the rebuilding of keys that depend on it.
\end{itemize}

In both of the above cases, we notify the parent \hs{reordering} function of
the outcome by returning \hs{Just}~\hs{result}. The astute reader may notice
that \hs{process} ignores \hs{dynamicDependencies} available in the \hs{result}.
While not required for \Excel, we have implemented build systems using
\hs{reordering} which use verifying and constructive traces, effectively turning
\Excel into a cloud build system and ensuring \hs{reordering} is not overly
fitted to \Excel alone.
%  (additional implementations will be submitted to the
% Artefact Evaluation).

In reality \Excel works slightly differently (as far as we are able to ascertain) --
on a change it propagates the dirty bits forward using the \hs{dynamicDependencies}, then only checks if the current \hs{key} is dirty. While both methods are equivalent, merely changing the interleaving, our approach allows us to model more of the behaviour of \Excel.

\vspace{-2mm}
\subsection{\Shake}\label{sec-implementation-shake}
\vspace{-1mm}

The \Shake approach for dependency tracking involves recording traces and verifying them, for which we use the \hs{Trace} type defined in \ref{sec-verifying-traces}. Complete code is given in \S\ref{fig-shake-implementation}, split into three functions:

\begin{description}
\item[\hs{traceMatch}] takes a list of all recorded \hs{Trace} values, the \hs{key} you are currently building, and a function \hs{check} which checks a specified dependency. It returns the \hs{result} field of all traces that match. Since \hs{check} is in an arbitrary monad, the function has to use \hs{allM}/\hs{&&^} instead of the more usual \hs{all}/\hs{&&} functions\footnote{These functions are all available in the \cmd{extra} library on Hackage.}.
\item[\hs{recursive}] defines the dependency ordering pattern. It requires a \hs{process} function that builds a key given a way to recursively build a dependency, and a way to run task to produce result. The main purpose of \hs{recursive} is to ensure that in a single run no key is built twice~--~for which it extends the \hs{State} monad with a list of \hs{done} keys.
\item[\hs{shake}] ties everything together. First it tests if the traces allow the current state of the target key. If not, it builds the key and updates the store. The only subtlety is that Shake calls \hs{fetch} on the keys while checking them~--~building the last-recorded dependencies before checking them. One minor annoyance is that the \hs{State} has been extended and thus needs to be projected using \hs{fst} before it is used.
\end{description}

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
-- Shake build system
shake :: (Ord k, Hashable v) => Build Monad (VT k v) k v
shake = recursive vtRebuilder
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Recursive dependency strategy
recursive :: Ord k => Rebuilder Monad i k v -> Build Monad i k v
recursive rebuilder tasks key store = fst $ execState (fetch key) (store, Set.empty)
  where
    fetch :: k -> State (Store i k v, Set k) v
    fetch key = case tasks key of
        Nothing -> gets (getValue key . fst)
        Just task -> do
            done <- gets snd
            when (key `Set.notMember` done) $ do
                value <- gets (getValue key . fst)
                let newTask :: Task (MonadState i) k v
                    newTask = rebuilder key value task
                    newFetch :: k -> StateT i (State (Store i k v, Set k)) v
                    newFetch k = do v <- lift $ fetch k
                                    put =<< lift (gets (getInfo . fst))
                                    return v
                info <- gets (getInfo . fst)
                (newValue, newInfo) <- runStateT (run newTask newFetch) info
                modify $ \(s, _) ->
                    (putInfo newInfo $ putValue key newValue s, Set.insert key done)
            gets (getValue key . fst)
\end{minted}
\vspace{1mm}
\begin{minted}[fontsize=\small]{haskell}
-- Rebuilder based on verifying traces
vtRebuilder :: (Eq k, Hashable v) => Rebuilder Monad (VT k v) k v
vtRebuilder key value task = Task $ \fetch -> do
    vt <- get
    dirty <- not <$> verifyVT key value (fmap hash . fetch) vt
    if not dirty
    then return value
    else do
        (newValue, deps) <- trackM task fetch
        put =<< recordVT key newValue deps (fmap hash . fetch) =<< get
        return newValue
\end{minted}
\vspace{-3mm}
\caption{An implementation of \Shake using our framework.}\label{fig-shake-implementation}
\vspace{1mm}
\end{figure}

\vspace{-2mm}
\subsection{\Bazel}\label{sec-implementation-bazel}
\vspace{-1mm}

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
bazel :: (Eq k, Hashable v) => Build Applicative (Traces k v) k v
bazel = topological $ \key ds act -> do
    s <- get
    let Traces traces contents = getInfo s
    poss <- traceMatch (\k v -> return $ getHash k s == v) key traces
    if null poss then do
        v <- act
        modify $ \s ->
            let t = Trace key [(d, getHash d s) | d <- ds] (getHash key s)
                ts = Traces (t : traces) (Map.insert (hash v) v contents)
            in putInfo ts (putValue key v s)
    else do
        when (getHash key s `notElem` poss) $
            modify $ putValue key (contents Map.! head poss)
\end{minted}
\vspace{-2mm}
\caption{An implementation of \Bazel using our framework; \hs{topological} is
defined in Fig.~\ref{fig-make-implementation}.}\label{fig-bazel-implementation}
\vspace{-2mm}
\end{figure}

Now we have seen all three dependency schemes, we can directly reuse \hs{topological} to define \Bazel. Furthermore, as \Bazel is a tracing build system, we can reuse \hs{Trace} and \hs{traceMatch}, along with the \hs{Traces} type from \S\ref{sec-constructive-traces}. With these pieces in place, the implementation of \Bazel is given in Fig.~\ref{fig-bazel-implementation}. We first figure out the possible results given the current state. If there are no recorded traces for the current dependencies we run it and record the results, otherwise grab a suitable result from the \hs{contents} cache.

The program presented above captures certain aspects of \Bazel, but the real implementation makes one important additional assumption on \hs{Task} -- namely that it is \textit{deterministic}. With that assumption \Bazel is able to create the result hash in a trace by hashing together the result hashes of the dependencies and the key -- as the mapping between dependencies and results is deterministic. As a consequence \Bazel can compute the results of traces locally, without looking at \hs{Traces} (potentially saving a roundtrip to the server). To model this change in the code would require storing an additional transient piece of information \hs{done} of type \hs{Map}~\hs{k}~\hs{(Hash}~\hs{v)}, storing the computed hashes so far this run. With that available, \hs{getHash}~\hs{key}~\hs{s} would become:
\begin{minted}[xleftmargin=10pt]{haskell}
hash (key, [ done Map.! d | d <- ds ])
\end{minted}
And that new hash would have to be stored in \hs{done}.

\subsection{Cloud \Shake}\label{sec-implementation-cloud-shake}

Using the abstractions and approaches built thus far, we have shown how to combine dependency scheme and change approach to reproduce existing build systems. In the attached materials we have implemented 9~build systems corresponding to all three dependency schemes, matched with all three change approaches. To us, the most interesting build system as yet unavailable would matching recursive ordering with constructive traces~--~providing a cloud-capable build system with minimality, cutoff and monadic dependencies. Using our framework it is possible to define and test such a system as per Fig.~\ref{fig-cloudshake-implementation}.

\begin{figure}
\begin{minted}[fontsize=\small]{haskell}
cloudShake :: (Eq k, Hashable v) => Build Monad (Traces k v) k v
cloudShake = recursive $ \key fetch act -> do
    s <- gets fst
    let Traces traces contents = getInfo s
    poss <- traceMatch (\k v -> (==) v . hash <$> fetch k) key traces
    if null poss then do
        (v, ds) <- act
        modify $ \(s,done) ->
            let t = Trace key [(d, getHash d s) | d <- ds] (getHash key s)
                ts = Traces (t : traces) (Map.insert (hash v) v contents)
            in (putInfo ts (putValue key v s), done)
    else do
        s <- gets fst
        when (getHash key s `notElem` poss) $
            modify $ \(s, done) -> (putValue key (contents Map.! head poss) s, done)
\end{minted}
\vspace{-2mm}
\caption{An implementation of Cloud \Shake using our framework.}\label{fig-cloudshake-implementation}
\vspace{-4mm}
\end{figure}

The differences from \hs{bazel} are minor~--~the dependency scheme has changed
from \hs{topological} to \hs{recursive}, and thus the dependency keys \hs{dk}
are captured from the action rather than in advance, the transient state has
gained a list of keys, and the checking calls \hs{fetch} to get the result
instead of accessing the store directly.

\vspace{-1mm}
\subsection{Smarter \hs{[Trace]} data structures}\label{sec-smart-traces}
\vspace{-1mm}

In the examples above, we have used \hs{[Trace}~\hs{k}~\hs{v]} to capture a list
of traces~--~however, using a list necessarily means that finding the right trace
takes $O(n)$. For each of the \hs{Trace} based systems it is possible to devise
a smarter representation, which we sketch below. Note that these implementations
do not avoid calls to \hs{compute}, merely overheads in the build system itself.

\begin{enumerate}
\item Any system using verifying traces, e.g. \Shake, is unlikely to see significant benefit from storing more than one \hs{Trace} per key\footnote{There is a small chance of a benefit if the dependencies change but the result does not, and then the dependencies change back to what they were before.}. Therefore, such systems can store \hs{Map}~\hs{k}~\hs{(Trace}~\hs{k}~\hs{v)}, where the initial \hs{k} is the \hs{key} field of \hs{Trace}.
\item Any system using \hs{Applicative} dependencies can omit the dependency keys from the \hs{Trace} as they can be recovered from the \hs{key} field.
\item Any \hs{Applicative} build system storing constructive traces, e.g. \Bazel, can index directly from the key and results to the output result~--~i.e. \hs{Map}~\hs{(@@k,}~\hs{[Hash}~\hs{v])}~\hs{(Hash}~\hs{v)}. Importantly, assuming the traces are stored on a central server, the client can compute the key and the hashes of its dependencies, then make a single call to the server to retrieve the result hash. In this formulation we have removed the possibility for a single key/dependency state to map to multiple different hashes, e.g. on a non-deterministic build~--~something \Bazel already prohibits which is discussed more in~\S\ref{sec-non-determinism}.
\item Finally, a \hs{Monad} build system with constructive traces can be stored as \hs{Map}~\hs{k}~\hs{(Choice}~\hs{k}~\hs{v)}, assuming a definition of \hs{Choice} as:
\begin{minted}[xleftmargin=10pt]{haskell}
data Choice k v = Choice k (Map (Hash v) (Choice k v))
                | Result (Hash v)
\end{minted}
Here the \hs{Choice} encodes a tree, asking successive questions about keys, and taking different branches based on the answers, until it reaches a final result. Implementing this structure over client-server communication requires either a chatty interface with lots of round-trips per \hs{Choice} step, or sending over a part of the tree that is not subsequently explored.
\end{enumerate}
